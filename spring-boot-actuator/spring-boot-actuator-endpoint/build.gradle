buildscript {
    ext {
        springBootVersion = '1.5.14.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    group = 'com.hirakida.spring.boot'
    version = '0.0.1-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'org.springframework.boot'

    sourceCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
        maven { url "file://${rootProject.projectDir}/mvn-repo" }
    }
}

project(':hello-endpoint-autoconfigure') {
    bootRepackage.enabled = false

    dependencies {
        compile('org.springframework.boot:spring-boot-starter-web')
        compile('org.springframework.boot:spring-boot-starter-actuator')
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "file://${rootProject.projectDir}/mvn-repo")
            }
        }
    }
}

project(':hello-endpoint-example') {
    dependencies {
        compile('org.springframework.boot:spring-boot-starter-web')
        compile('org.springframework.boot:spring-boot-starter-actuator')
        compile('org.springframework.boot:spring-boot-actuator-docs')
        compile("com.hirakida.spring.boot:hello-endpoint-autoconfigure:${version}")
    }
}
