buildscript {
    ext {
        springBootVersion = '2.0.5.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

allprojects {
    group = 'com.github.hirakida.spring.boot'
    version = '0.0.1-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
        maven { url "file://${rootProject.projectDir}/maven-repo" }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "file://${rootProject.projectDir}/maven-repo")
            }
        }
    }
}

project(':hello-spring-boot-autoconfigure') {
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        annotationProcessor('org.springframework.boot:spring-boot-configuration-processor')
        compileOnly('org.springframework.boot:spring-boot-configuration-processor')
        compile('org.springframework.boot:spring-boot-starter')
        testCompile('org.springframework.boot:spring-boot-starter-test')
    }
}

project(':hello-spring-boot-starter') {
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        compile("com.github.hirakida.spring.boot:hello-spring-boot-autoconfigure:${version}")
        compile('org.springframework.boot:spring-boot-starter')
    }
}
