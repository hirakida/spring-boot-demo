plugins {
    id 'org.springframework.boot' version '2.1.7.RELEASE' apply false
    id 'com.google.protobuf' version '0.8.10' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.google.protobuf'

    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenCentral()
    }

    protobuf {
        protoc {
            artifact = 'com.google.protobuf:protoc:3.9.1'
        }
        plugins {
            grpc {
                artifact = 'io.grpc:protoc-gen-grpc-java:1.23.0'
            }
        }
        generateProtoTasks {
            ofSourceSet('main')*.plugins {
                grpc {}
            }
        }
    }
    sourceSets {
        main {
            proto {
                srcDir "${rootProject.rootDir}/proto"
            }
        }
    }

    dependencies {
        implementation 'io.github.lognet:grpc-spring-boot-starter:3.4.1'
    }
}

project(':grpc-server') {
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        runtimeOnly 'org.springframework.boot:spring-boot-devtools'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }
}

project(':grpc-client') {
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }
}
