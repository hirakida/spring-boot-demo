buildscript {
    ext {
        springBootVersion = '2.0.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    group 'com.example'
    version '0.0.1-SNAPSHOT'
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compile('org.springframework.kafka:spring-kafka')
        annotationProcessor('org.projectlombok:lombok')
        compileOnly('org.projectlombok:lombok')
    }
}

project(':kafka-common') {
    bootJar.enabled = false
    jar.enabled = true
}

project(':kafka-producer') {
    dependencies {
        compile project(':kafka-common')
        compile('org.springframework.boot:spring-boot-starter-web')
        runtime('org.springframework.boot:spring-boot-devtools')
        testCompile('org.springframework.boot:spring-boot-starter-test')
        testCompile('org.springframework.kafka:spring-kafka-test')
    }
}

project(':kafka-consumer') {
    dependencies {
        compile project(':kafka-common')
        compile('org.springframework.boot:spring-boot-starter-web')
        runtime('org.springframework.boot:spring-boot-devtools')
        testCompile('org.springframework.boot:spring-boot-starter-test')
        testCompile('org.springframework.kafka:spring-kafka-test')
    }
}
