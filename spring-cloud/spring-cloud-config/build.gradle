buildscript {
    ext {
        springBootVersion = '1.5.11.RELEASE'
    }
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/plugins-release' }
        maven { url 'https://repo.spring.io/libs-milestone' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('io.spring.gradle:propdeps-plugin:0.0.9.RELEASE')
    }
}

allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-idea'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/libs-milestone' }
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-config:1.4.3.RELEASE'
        }
    }

    dependencies {
        annotationProcessor('org.springframework.boot:spring-boot-configuration-processor')
        annotationProcessor('org.projectlombok:lombok')
        compileOnly('org.projectlombok:lombok')
    }
}

project(':config-client') {
    dependencies {
        compile('org.springframework.cloud:spring-cloud-starter-config')
        compile('org.springframework.boot:spring-boot-starter-web')
        compile('org.springframework.boot:spring-boot-starter-actuator')
        runtime('org.springframework.boot:spring-boot-devtools')
        optional('org.springframework.boot:spring-boot-configuration-processor')
        testCompile('org.springframework.boot:spring-boot-starter-test')
    }

    compileJava.dependsOn(processResources)
}

project(':config-server') {
    dependencies {
        compile('org.springframework.cloud:spring-cloud-config-server')
    }
}
