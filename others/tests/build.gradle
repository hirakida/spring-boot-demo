plugins {
    id 'org.springframework.boot' version '2.3.3.RELEASE' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        dependencies {
            dependencySet(group: 'com.intuit.karate', version: '0.9.6') {
                entry 'karate-junit5'
                entry 'karate-apache'
            }
            dependencySet(group: 'org.spockframework', version: '1.3-groovy-2.5') {
                entry 'spock-core'
                entry 'spock-spring'
            }
            dependency 'org.codehaus.groovy:groovy-all:2.5.13'
        }
    }

    dependencies {
        annotationProcessor 'org.projectlombok:lombok'
        compileOnly 'org.projectlombok:lombok'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    test {
        useJUnitPlatform()
    }
}
