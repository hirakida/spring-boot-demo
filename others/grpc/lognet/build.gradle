plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.8.RELEASE' apply false
    id 'com.google.protobuf' version '0.8.11' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        dependencies {
            dependencySet(group: 'io.grpc', version: '1.26.0') {
                entry 'grpc-netty-shaded'
                entry 'grpc-protobuf'
                entry 'grpc-stub'
            }
            dependency 'io.github.lognet:grpc-spring-boot-starter:3.5.1'
            dependency 'javax.annotation:javax.annotation-api:1.3.2'
        }
    }
}

project(':protobuf') {
    apply plugin: 'com.google.protobuf'

    protobuf {
        protoc {
            artifact = 'com.google.protobuf:protoc:3.11.2'
        }
        plugins {
            grpc {
                artifact = 'io.grpc:protoc-gen-grpc-java:1.26.0'
            }
        }
        generateProtoTasks {
            ofSourceSet('main')*.plugins {
                grpc {}
            }
        }
    }

    dependencies {
        implementation 'io.grpc:grpc-netty-shaded'
        implementation 'io.grpc:grpc-protobuf'
        implementation 'io.grpc:grpc-stub'
        implementation 'javax.annotation:javax.annotation-api'
    }
}

project(':grpc-client') {
    apply plugin: 'org.springframework.boot'

    dependencies {
        implementation project(':protobuf')
        implementation 'io.github.lognet:grpc-spring-boot-starter'
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }
}

project(':grpc-server') {
    apply plugin: 'org.springframework.boot'

    dependencies {
        implementation project(':protobuf')
        implementation 'io.github.lognet:grpc-spring-boot-starter'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }
}
